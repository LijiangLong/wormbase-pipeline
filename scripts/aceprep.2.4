#!/bin/csh 
#
# aceprep2
# script to make an ace file for a cosmid - trnascan and blast
# should be run on Silicon Graphics
# prosite_homology added 940504
# make fasta format sequence added 941102
# logfile and status outputs added 941111
# changed to swir9 951207
# added tblastx searching against C. elegans ests
# 960301 compressed tblastx EST C. elegans
# 960329 added searching against Tc1 database
# 960425 added removal of assembly tags
# 960521 changed to swir10. 
# 960521 changed to tblastx nematode ESTs and stopped 
#	tblastx'ing gbest-no-elegans and CBESTs 
# 960819 Changed to swir11.
# 961018 Added author info 
# 961207 changed so that blastx searches $swir not swir 
# 970312 Changed so that elegans ESTs are mapped using est_genome
# 970807 Changed to tblastx against C. briggsae data 
# 971112 Added -ci switch to est_genome2ace 
# 980126 Changed repeat family lookup table to use new CeRep families

# WANT THE ACEPREP2 SCRIPT TO RUN AS A CHILD 
# SO THAT THE EXIT STATUS CAN BE OBTAINED
############################################

if (! $?child) then

#Check correct architecture and arguments

if  ($#argv != 2) then 
  echo 'Usage: aceprep2 COSMID USER_NAME'
  exit 1
endif

if (! -e $1.seq) then
  echo "You must be working in a directory containing $1.seq"
  exit 1
endif

if ( `uname` != IRIX && `uname` != OSF1 && `uname` != Linux) then
  echo "You must run this script on an SGI or DEC machine."
  exit 1
endif

#start up child process
  setenv child 1

  if (! -e analysis_logfile) touch analysis_logfile  
  
  $0 $* >&  errors$$

  if ($status != 0) then 
 
    echo `date +%H:%M:%S` " : ACEPREP2 FAILED " >> analysis_logfile
  else
    echo `date +%H:%M:%S` " : ACEPREP2 COMPLETED " >> analysis_logfile	
  endif

#report errors to logfile

if (-z errors$$) then 
	echo "NO ERRORS REPORTED" >> analysis_logfile
else
  echo "  ERRORS" >> analysis_logfile
  cat errors$$ >> analysis_logfile
endif  
\rm errors$$



  echo "" >> analysis_logfile

#mail the report to the user - may not want to make this a perm.
#feature -see what the response to this is
set dir = `pwd`
mail -s "$1 aceprep" $2 < $dir/analysis_logfile

exit

endif


#TRUE BEGINNING OF ACEPREP2 #
#############################


set acefile = $1_2.ace
set swir = swir13
set dat = `date +%y%m%d` 



#WRITE DETAILS OF THIS SESSION TO LOGFILE
#############################################

if (! -e analysis_logfile) touch analysis_logfile

echo "" >> analysis_logfile
echo "Script: ACEPREP2" >> analysis_logfile
echo "Sequence: $1" >> analysis_logfile
echo "Started on: $dat" >> analysis_logfile
echo "Time: `date +%H:%M:%S`" >> analysis_logfile
echo "Run on machine: $HOST" >> analysis_logfile
echo "Executed by: $2" >> analysis_logfile 
echo "Process ID: $$" >> analysis_logfile


#MAKE FASTA FORMAT OF THE SEQUENCE#
###################################


echo -n ">" >! $1.fasta
echo $1 >> $1.fasta
cat $1.seq >> $1.fasta

#MAKE A BLAST DIRECTORY IF REQUIRED#
####################################


if (! -d blast) then
  mkdir blast
endif

#ADD AUTHOR INFO#
#################

#this creates the $acefile!!
get_author_ace $1  >! $acefile
if ($status == 1) then 
    echo `date +%H:%M:%S` " : Could not find a reference for the author -aborted" >> analysis_logfile
    exit 1
else 
	echo `date +%H:%M:%S` " : Added Author info" >> analysis_logfile 
endif

 
#EST_genome Celegans ESTs#
##########################


echo `date +%H:%M:%S` " : Running est_genome2ace against C.elegans ESTs" >> analysis_logfile 
est_genome2ace -ci ~wormpub/analysis/cdna/allcdna $1.fasta >>  $acefile
echo `date +%H:%M:%S` " : cDNA hits added to $acefile : exit status=$status" >> analysis_logfile  


#TBLASTX Celegans ESTs#
######################

if (! -e blast/$1.cdna.tblastx && ! -e blast/$1.cdna.tblastx.gz) then
  echo `date +%H:%M:%S` " : Running tblastx against C.elegans ESTs" >> analysis_logfile 
  
    #I have no idea why but this keeps failing if sent to the queue so will not send to queue. 
    #bsub -I -q local_blastq -R blast tblastx ~wormpub/analysis/cdna/allcdna $1.fasta  > blast/$1.cdna.tblastx 
    tblastx ~wormpub/analysis/cdna/allcdna $1.fasta  > blast/$1.cdna.tblastx 
    
  echo `date +%H:%M:%S` " : cDNAsearch completed : exit status=$status" >> analysis_logfile
else 
  echo `date +%H:%M:%S` " : cDNAsearch not executed, tblastx file already exists" >> analysis_logfile 
endif

echo `date +%H:%M:%S` " : Adding cdna hits to ace file" >> analysis_logfile
if (-e blast/$1.cdna.tblastx.gz) gzip -d blast/$1.cdna.tblastx.gz
MSPcrunch -4M blast/$1.cdna.tblastx | sed 's/TBLASTX/TBLASTX_EST_elegans/'  >> $acefile
echo `date +%H:%M:%S` " : tblastx DNA hits added to $acefile : exit status=$status" >> analysis_logfile
echo "" >> $acefile
gzip blast/$1.cdna.tblastx


#TBLASTX  against C. briggsae#
##############################

if (! -e blast/$1.tblastx.allC.briggsae.gz && ! -e blast/$1.tblastx.allC.briggsae ) then 
echo `date +%H:%M:%S`  " : Running tblastx against C.briggsae genomic " >> analysis_logfile 
bsub -I -q local_blastq -R blast tblastx ~wormpub/BRIGGSAE/Sequence_databases/allbriggsae $1.fasta > blast/$1.tblastx.allC.briggsae
echo `date +%H:%M:%S` " : TBlastx against C.briggsae completed : exit status=$status" >> analysis_logfile
else 
echo `date +%H:%M:%S` " : tblastx against C. briggsae  not executed, tblastx file already exists" >> analysis_logfile 
endif 

if (-e blast/$1.tblastx.allC.briggsae.gz) gzip -d blast/$1.tblastx.allC.briggsae.gz

echo `date +%H:%M:%S` " : Running MSPcrunch on C.briggsae tblastx " >> analysis_logfile
MSPcrunch -4 blast/$1.tblastx.allC.briggsae  | sed 's/TBLASTX/TBLASTX_C.briggsae/' >>  $acefile
echo `date +%H:%M:%S` " : C. briggsae database tblastx hits added to $acefile : exit status=$status" >> analysis_logfile
echo "" >> $acefile

gzip blast/$1.tblastx.allC.briggsae

#Blastn versus Tc1 database#
############################
 
echo `date +%H:%M:%S` " : Running blastn against Tc1 database" >> analysis_logfile 
bsub -I -q local_blastq -R blast blastn ~wormpub/analysis/tc1/PK.dna.fasta $1.fasta | MSPcrunch -4 - | sed 's/BLASTN/BLASTN_TC1/' >>  $acefile
echo `date +%H:%M:%S` " : Tc1 database hits added to $acefile : exit status=$status" >> analysis_logfile
echo "" >> $acefile

#tRNA scan#
###########


if (! -e tRNA/$1.tRNA) then
  echo `date +%H:%M:%S` " : Running trnascan" >> analysis_logfile
  mkdir tRNA
  cd tRNA
  echo ';' >! $1
  cat ../$1.seq >> $1
  ln -s ~pubseq/src/trnascan/TPCsignal .
  ln -s ~pubseq/src/trnascan/Dsignal .
  trnascan $1 $1.tRNA >& /dev/null
  echo `date +%H:%M:%S` " : trnascan run : exit status=$status " >> ../analysis_logfile
  cd ..
else
  echo `date +%H:%M:%S` " : trnascan not run, output file already exists" >> analysis_logfile
endif
trnascan2ace cosmid=$1 tRNA/$1.tRNA >> $acefile
echo `date +%H:%M:%S` " : Added tRNAs to $acefile : exit status=$status " >> analysis_logfile


# BLAST SWIR#
#############


if (! -e blast/$1.$swir.gz && ! -e blast/$1.$swir ) then
  echo `date +%H:%M:%S`  " : Running blastx " >> analysis_logfile 
    bsub -I -q local_blastq -R blast blastx $swir $1.fasta B=1000000 -span1  M=BLOSUM62-12 V=0 H=0 >! blast/$1.$swir 
  echo `date +%H:%M:%S` " : Blastx run : exit status=$status" >> analysis_logfile
  if (-e blast/$1.exblx) \rm blast/$1.exblx
else
  echo `date +%H:%M:%S` " : Blastx not run, blastx file already exists" >> analysis_logfile 	
endif


if (-e blast/$1.$swir.gz) gzip -d blast/$1.$swir.gz

  echo `date +%H:%M:%S` " : Running MSPcrunch" >> analysis_logfile
  MSPcrunch -x blast/$1.$swir >! blast/$1.exblx
  echo `date +%H:%M:%S` " : MSPcrunch completed : exit status=$status" >> analysis_logfile 

echo `date +%H:%M:%S` " : Adding blastx hits to ace file" >> analysis_logfile

exblx2ace_4  $1 Pep_homol DNA_homol BLASTX  blast/$1.exblx >> $acefile
echo `date +%H:%M:%S` " : Completed exblx2ace_4  : exit status=$status" >> analysis_logfile

gzip blast/$1.$swir


#TBLASTX EST NEMATODE DATABASE WITH CELEGANS ESTs REMOVED
####################################################################

if (! -e blast/$1.ests.tblastx) then
  echo `date +%H:%M:%S` " : Running tblastx versus non-elegans nematode ESTs" >> analysis_logfile
  bsub -I -q local_blastq -R blast tblastx  ~wormpub/analysis/ESTs/non_C.elegans_nematode_ESTs  $1.fasta >! blast/$1.ests.tblastx
  echo `date +%H:%M:%S` " : TBlastx completed : exit status=$status" >> analysis_logfile
  if (-e blast/$1.ests.exblx) \rm blast/$1.ests.exblx
else 
	echo `date +%H:%M:%S` " : TBlastx versus non-elegans nematode ESTs not run, tblastx file already exists " >> analysis_logfile
endif

echo `date +%H:%M:%S` " : Converting blastfile file to ace format " >> analysis_logfile
MSPcrunch -x blast/$1.ests.tblastx  >! blast/$1.est.exblx 
exblx2ace_4  $1 DNA_homol DNA_homol TBLASTX_EST blast/$1.est.exblx >> $acefile
echo `date +%H:%M:%S` " : Completed adding blast file to acefile: exit status=$status" >> analysis_logfile



#TBLASTX C. Briggsae ESTs - may not need this in the future when these ESTS are in gbest#
# to do this though the CB:* seq objects in acedb could then be removed
#########################################################################################

##
#
#if (! -e blast/$1.CBESTs.tblastx) then
#  echo `date +%H:%M:%S` " : Running tblastx versus C.Briggsae ESTs" >> analysis_logfile
#  tblastx CBESTs $1.fasta >! blast/$1.CBESTs.tblastx
#  echo `date +%H:%M:%S` " : TBlastx versus C.Briggsae ESTs completed : exit status=$status" >> analysis_logfile
#  if (-e blast/$1.CBESTs.exblx) \rm blast/$1.CBESTs.exblx
#else 
# 	echo `date +%H:%M:%S` " : TBlastx versus C. Briggsae ESTs not run, tblastx file already exists " >> analysis_logfile
#endif
#
#if (! -e blast/$1.CBESTs.exblx) then
# 
#  MSPcrunch -x  blast/$1.CBESTs.tblastx > blast/$1.CBESTs.exblx
#  echo `date +%H:%M:%S` " : MSPcrunch completed of tblastx C.Briggsae ESTs file : exit status=$status" >> analysis_logfile
#else 
#  echo `date +%H:%M:%S` " : MSPcrunch of C. Brigsae ESTs tblastx file not run, exblx  file exists " >> analysis_logfile
#endif
#
#
#
#echo `date +%H:%M:%S` " : Converting exblx file to ace format " >> analysis_logfile
#exblx2ace_4  $1 DNA_homol DNA_homol TBLASTX_EST  blast/$1.CBESTs.exblx >> $acefile
#echo `date +%H:%M:%S` " : Completed exblx2ace_4 : exit status=$status" >> analysis_logfile
#


#TBLASTX VERSUS STLANDCAM-CMID#          
##############################

#if  (! -e blast/$1.allcmid.exblx) then
# echo `date +%H:%M:%S` " : Running tblastx versus stl and cam cmids" >> analysis_logfile
# tblastx ~wormpub/analysis/Sequence_databases/stlandcam-cmid $1.fasta | MSPcrunch - >! blast/$1.allcmid.exblx
# echo `date +%H:%M:%S` " : TBlastx versus stl and cam cmids completed and exblx file created : exit status=$status" >> analysis_logfile	 
#else echo `date +%H:%M:%S` " : TBlastx versus stl and cam cmids not run, exblx file already exists " >> analysis_logfile
#endif 

#echo `date +%H:%M:%S` " : Converting exblx file to ace format " >> analysis_logfile
#exblx2ace_4  $1 DNA_homol DNA_homol TBLASTX_C.elegans-genomic  blast/$1.allcmid.exblx >> $acefile	
#echo `date +%H:%M:%S` " : Completed exblx2ace_4 : exit status=$status" >> analysis_logfile


# Add prosite homology hits #
#############################

# Have switched this function off (960109) as the current
# PSsearch implementation cannot cope with the current database size. 
# Switched prosite on (960205) to use the perl script for searching prosite database


if (! -e prosite) mkdir prosite

echo `date +%H:%M:%S` " : Adding Prosite hits to $acefile" >> analysis_logfile
prosite_homology_4 $1 >> $acefile 
echo `date +%H:%M:%S` " : Prosite hits added to $acefile : exit status=$status" >> analysis_logfile


# Search for repeat families #
##############################

echo "" >>  $acefile
echo `date +%H:%M:%S` " : Searching repeat families, adding to $acefile" >> analysis_logfile
Search_Repeat_Families $1.fasta ~wormpub/analysis/Repeat_Families/Repeat_Families_lookup_table  >>  $acefile 
echo `date +%H:%M:%S` " : Repeat families added to $acefile : exit status=$status" >> analysis_logfile
echo "" >>  $acefile


#Add the EST sequences to the acefile# 
######################################

#blastn ~wormpub/analysis/cdna/allcdna $1.fasta H=0 V=0 >! blast/$1.cdna.blastn 
#blastn ~wormpub/analysis/cdna/allcdna $1.fasta H=0 V=0 | MSPcrunch -x - >! blast/$1.cdna.blastn.exblx
#cat blast/$1.cdna.blastn.exblx | perl -ne '/(\S+)\s+$/;print "$1\n";'|sort -u -| perl -ne 'chop; system("echo\;agrep -d \"\\>\" $_ /nfs/disk100/wormpub/analysis/cdna/allcdna");' | perl -ne  's/>/\nDNA /;print;' >> $acefile
#echo "" >> $acefile

#MAKE EMBL DIRECTORY#
#####################

if (! -e embl) mkdir embl


#BLASTN AGAINST CMID#
#####################

#Switched off 960329 to conserve disk space 
#echo `date +%H:%M:%S` " : Blasting cosmid sequence vs all other cosmids (Results file=$1.cmid.gz)" >> analysis_logfile
#blastn ~wormpub/analysis/cosmids/cmid $1.fasta >! $1.cmid
#if (-e $1.cmid.gz) then
#	\rm $1.cmid.gz
#endif
#gzip $1.cmid
#echo `date +%H:%M:%S` " : Blast vs cmid complete : exit status=$status" >> analysis_logfile


#MAKE A LOCALACE VERSION#
#must appear after all programs which write to ace files#
#########################################################
#I am stopping premaking an acedb database as they will now 
#be made on the fly 

#echo `date +%H:%M:%S` " : Making localace" >>  analysis_logfile
#mklocalace >& /dev/null 
#echo `date +%H:%M:%S` " : mklocalace complete : exit status=$status" >> analysis_logfile



# DOTTER #
##########

if (! -e $1.dotter.gz && ! -e $1.dotter) then
  echo `date +%H:%M:%S` " : Running dotter, you dont have to wait for this to finish to run localace" >> analysis_logfile
  nice dotter -b $1.dotter $1.seq $1.seq > /dev/null
  echo `date +%H:%M:%S` " : Dotter complete : exit status=$status" >> analysis_logfile
  gzip  $1.dotter
endif


#REMOVE THE ASSEMBLY TAGS WHICH AREN'T WANTED#
##############################################

rmassemblytags $1 >> $acefile









