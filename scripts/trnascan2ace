#!/usr/local/bin/gawk -f
# make ace files from trnascan output
# Usage: trnascan2ace cosmid=<cosmid name> cosmid.tRNA

/^start position=/ { 

	#Avoid duplications - use first prediction (correct?)
	if ($3 == start && $6 == end) duplicate = 1
	else duplicate = 0

	intronstart = intronend = 0

	start = $3
	end   = $6 
	if (start < end) plusmin = 1;
	else plusmin = -1
}

/^tRNA predict as a tRNA-/ {

	type = $6
}

/^potential intron between positions/ {

	intronstart = $5
	intronend   = $6
}

/^number of base pairing in the anticodon stem=/ { # End of record

	if (duplicate) next

	nr++

	trna = cosmid ".t" nr

	#Report this one
	print "Sequence", cosmid
	print "Subsequence", trna, start, end
	print ""

	print "Sequence :", trna
	print "Source", "\"" cosmid "\""
	if (intronstart) 
	{
		printf("Source_Exons 1 %d\n", plusmin*(intronstart - start))
		printf("Source_Exons %d %d\n", plusmin*(intronend - start)+1, plusmin*(end-start)+1)
	}
	print "tRNA", type
	print ""
}
