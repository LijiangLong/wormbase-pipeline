#!/bin/csh
#
# aceprep
# script to make a genefinding ace file for a cosmid
#
# added date_directory field 941118



switch ($#argv)
  case "1":
        breaksw
  default:
        echo 'Usage: aceprep COSMID' ; exit 1
endsw

if (! -e $1.seq) then
  echo "You must be working in a directory containing $1.seq"
  exit 1
endif

if ( `uname` != SunOS ) then
  echo "You must run his script on a SUN machine"
  exit 1
endif


set acefile = $1_1.ace
set directory = `pwd | sed 's:.*/::' | sed s/u// `

if (! -e $1_0.ace ) then
# Don't put DNA sequence info in if it already exits in $1_0.ace
  cat >! $acefile << END

DNA $1
END


  echo "Adding DNA"
  cat $1.seq >> $acefile

else
  touch $acefile
endif

cat >> $acefile <<END

Sequence $1
Genomic_canonical
From_Laboratory CB
Clone $1
Date_directory $directory
END


if (! -e gf/gf.out) then
  echo "Running genefinder"
  gfprep $1
endif
echo "Adding genefinder predictions"
sed 's/\\\//, /g' < gf/gf.out | gf2ace >> $acefile


if (! -d repeats) then
  echo "Running repeatsearch"
  repeatsearch $1
endif
echo "Adding repeats"
exinv < repeats/$1.inv | inv2ace >> $acefile
tan2ace < repeats/$1.tan >> $acefile


exit 0

















